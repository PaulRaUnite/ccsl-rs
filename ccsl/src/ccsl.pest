file = { SOI ~ specification ~ EOI}
specification = {"Specification" ~ id ~ "{" ~ clocks ~ ("[" ~ constraint+ ~ "]")? ~"}"}
clocks = {"Clock" ~ id ~ ("," ~ id)*}
constraint = _{ repeat | causal_chain | subclock_chain | exclusion_chain | let_expr | periodic_def }

causal_chain = {"Precedence" ~ id ~ (causality_kind ~ causal_params? ~ id)+ }
causality_kind = {"<=" | "<"}
causal_params = {"(" ~ "init:" ~ int ~ "max:" ~ infint ~ ")"}

repeat = { "repeat" ~ id ~ "every" ~ int? ~ id ~ from? ~ up_to? }
from = {"from" ~ int}
up_to = {"upTo" ~ infint}
periodic_def = { id ~ "=" ~ id ~ (delay | sampleOn | slice) }
delay = {"$" ~ int ~ ("on" ~id)?}
sampleOn = {"sampledOn" ~ id}
slice = {"[" ~ int ~ "," ~ infint ~ "]"}

subclock_chain = {"SubClocking" ~ id ~ ("<-" ~ id)*}

exclusion_chain = {"Exclusion" ~ id ~ ("#" ~ id)*}

let_expr = {"Let" ~ id ~ "be" ~ expression}
prefix_expr = {prefix_kind ~ "(" ~ id ~ ("," ~ id)* ~")"}
prefix_kind = {"inf" | "sup"}

operation = _{ union | intersection | minus }
union      = { "+" | "or" }
intersection = { "*" | "and" }
minus = { "-" | "minus" }

expression = {term ~ (operation ~ term)* }
term = _{ prefix_expr | "(" ~ expression ~ ")" | id  }

infint = {int | infinity}
infinity = @{"Infinity" | "âˆž"}
int = @{ ASCII_DIGIT+ }
id = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "-" | "_")* }
WHITESPACE = _{ " " | NEWLINE}